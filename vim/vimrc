let g:is_windows = has('win33')

" Load vim-plug {{{
if !g:is_windows && empty(glob($HOME . "/.vim/autoload/plug.vim"))
    :silent !mkdir -p ~/.vim/autoload >/dev/null 2>&1
    :silent !curl -fLo ~/.vim/autoload/plug.vim https://raw.github.com/junegunn/vim-plug/master/plug.vim >/dev/null 2>&1
endif
" }}}

" LOAD PLUGIN {{{
call plug#begin('~/.vim/plugged')

" file management
Plug 'ctrlpvim/ctrlp.vim' | Plug 'FelikZ/ctrlp-py-matcher'
Plug 'tpope/vim-eunuch'

" git
Plug 'tpope/vim-fugitive'

" textedit
Plug 'AndrewRadev/splitjoin.vim'
Plug 'jiangmiao/auto-pairs'
Plug 'bronson/vim-trailing-whitespace'
Plug 'junegunn/vim-easy-align', { 'on': '<Plug>(EasyAlign)' }
Plug 'haya14busa/incsearch.vim', { 'on': ['<Plug>(incsearch-forward)', '<Plug>(incsearch-backward)', '<Plug>(incsearch-stay)'] }
Plug 'tpope/vim-commentary'
Plug 'conradirwin/vim-bracketed-paste'
Plug 'tpope/vim-rsi'

Plug 'tpope/vim-sleuth'
Plug 'nathanaelkane/vim-indent-guides', { 'on': ['IndentGuidesToggle', 'IndentGuidesEnable'] }
Plug 'christoomey/vim-tmux-navigator'
Plug 'jgdavey/tslime.vim'
Plug 'sjl/gundo.vim', {'on': 'GundoToggle'}
Plug 'tpope/vim-abolish', { 'on': 'S' }
Plug 'tpope/vim-endwise'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-projectionist'
Plug 'mattn/emmet-vim', { 'for': ['html', 'haml', 'eruby'] }
Plug 'vim-scripts/fcitx.vim', { 'on': [] }
Plug 'SirVer/ultisnips', { 'on': [] }

Plug 'tpope/vim-dispatch'
Plug 'benekastah/neomake', { 'on': ['Neomake'] }
Plug 'janko-m/vim-test', { 'on': ['TestFile', 'TestLast', 'TestNearest', 'TestSuite', 'TestVisit'] }

Plug 'sheerun/vim-polyglot'

Plug 'tpope/vim-rails', { 'for': ['ruby', 'haml', 'eruby'] }
Plug 'ecomba/vim-ruby-refactoring', { 'on': ['RAddParameter', 'RInlineTemp',
            \ 'RConvertPostConditional', 'RExtractConstant', 'RExtractLet',
            \ 'RExtractLocalVariable', 'RRenameLocalVariable',
            \ 'RRenameInstanceVariable', 'RExtractMethod'] }

Plug 'ternjs/tern_for_vim'

" colorscheme
Plug 'xuanduc987/badwolf'

Plug 'junegunn/goyo.vim', { 'on': 'Goyo' }

if filereadable(expand("~/.vimplug.local"))
    source ~/.vimplug.local
endif
call plug#end()
" }}}

" create folder for swap, undo... {{{
if isdirectory($HOME . '/.vim/_temp') == 0
    :silent !mkdir -p ~/.vim/_temp >/dev/null 2>&1
endif
set directory=./.vim-swap//
set directory+=~/.vim/_temp//
set directory+=~/tmp//
set directory+=.

if has('nvim')
    set viminfo+=n~/.config/nvim/nvim.shada
else
    set viminfo+=n~/.vim/viminfo
endif

if exists("+undofile")
    if isdirectory($HOME . '/.vim/_undo') == 0
        :silent !mkdir -p ~/.vim/_undo > /dev/null 2>&1
    endif
    set undodir=./.vim-undo//
    set undodir+=~/.vim/_undo//
    set undofile
endif

" }}}

" sensible {{{
set autoindent
set backspace=indent,eol,start
set complete-=i
set smarttab

set nrformats-=octal

set ttimeout

set incsearch

set laststatus=2
set ruler
set wildmenu
set lazyredraw

set scrolloff=1
set sidescrolloff=5
set display+=lastline

if &encoding ==# 'latin1' && has('gui_running')
    set encoding=utf-8
endif

if v:version > 703 || v:version == 703 && has("patch541")
    set formatoptions+=j " Delete comment character when joining commented lines
endif

if has('path_extra')
    setglobal tags-=./tags tags-=./tags; tags^=./tags;
endif

set autoread

if &history < 1000
    set history=1000
endif
if &tabpagemax < 50
    set tabpagemax=50
endif
if !empty(&viminfo)
    set viminfo^=!
endif
set sessionoptions-=options

" Allow color schemes to do bright colors without forcing bold.
if &t_Co == 8 && $TERM !~# '^linux\|^Eterm'
    set t_Co=16
endif

" Load matchit.vim, but only if the user hasn't installed a newer version.
if !exists('g:loaded_matchit') && findfile('plugin/matchit.vim', &rtp) ==# ''
    runtime! macros/matchit.vim
endif
" }}}

" other
let mapleader = " "
set nowrap " don't wrap lines
set tabstop=8
set softtabstop=2 " a tab is two spaces
set shiftwidth=2 " an autoindent (with <<) is two spaces
set shiftround
set expandtab " use spaces, not tabs
set list " Show invisible characters
set hlsearch " highlight matches
set ignorecase " searches are case insensitive...
set smartcase " ... unless they contain at least one capital letter

set relativenumber

set modeline

set mouse=n " Enable mouse support
set cc=+1 " Color column textwidth + 1

set ttyfast
set timeout timeoutlen=500 ttimeoutlen=50
set synmaxcol=300

" Natural split
set splitbelow
set splitright

" Special chars
if (&termencoding ==# 'utf-8' || &encoding ==# 'utf-8') && version >= 700
    let &listchars = "tab:▸\ ,trail:·,extends:»,precedes:«,nbsp:\u26ad"
    let &fillchars = "vert:│,fold:·"
else
    set listchars=tab:>\ ,trail:-,extends:>,precedes:<
endif

" Disable output and VCS files
set wildignore+=*.o,*.out,*.obj,.git,*.rbc,*.rbo,*.class,.svn,*.gem
" Disable archive files
set wildignore+=*.zip,*.tar.gz,*.tar.bz2,*.rar,*.tar.xz
" Ignore bundler and sass cache
set wildignore+=*/vendor/gems/*,*/vendor/cache/*,*/.bundle/*,*/.sass-cache/*
" Ignore librarian-chef, vagrant, test-kitchen and Berkshelf cache
set wildignore+=*/tmp/librarian/*,*/.vagrant/*,*/.kitchen/*,*/vendor/cookbooks/*
" Ignore rails temporary asset caches
set wildignore+=*/tmp/cache/assets/*/sprockets/*,*/tmp/cache/assets/*/sass/*
" Disable temp and backup files
set wildignore+=*.swp,*~,._*

augroup restore_position
    autocmd!
    " Remember last location in file, but not for commit messages.
    " see :help last-position-jump
    au BufReadPost * if &filetype !~ '^git\c' && line("'\"") > 0 && line("'\"") <= line("$")
                \| exe "normal! g`\"" | endif
augroup END

" easy change buffer
nmap <leader>b :ls<CR>:b

" easy move between windows
nore <silent> <C-K> :wincmd k<CR>
nore <silent> <C-J> :wincmd j<CR>
nore <silent> <C-H> :wincmd h<CR>
nore <silent> <C-L> :wincmd l<CR>

" Adjust viewports to the same size
nore <Leader>= <C-w>=

" Toggle paste mode
nnore <silent> <F4> :set invpaste<CR>:set paste?<CR>
inore <silent> <F4> <ESC>:set invpaste<CR>:set paste?<CR>

" statusline
set statusline =
set statusline +=%m                          " modified flag
set statusline +=\ »
set statusline +=\ %f                        " Relative path to file
set statusline +=\ «
set statusline +=\ %h                        " [help]
set statusline +=%r                          " read only flag
set statusline +=%w                          " preview window flag
set statusline +=%y                          " filetype
set statusline +=%=%-14.(%l,%c%V%)           " Line, column-virtual column"
set statusline +=\ %P
function! StatusGitBranch()
    let b:s_git_branch_name = fugitive#head()
    if !empty(b:s_git_branch_name)
        return " ← ".b:s_git_branch_name
    else
        return " "
    endif
endfunction
set statusline +=%{StatusGitBranch()}

" Some helpers to edit mode
" http://vimcasts.org/e/14
cnoremap %% <C-R>=fnameescape(expand('%:h')).'/'<cr>

" Map the arrow keys to be based on display lines, not physical lines
nore <Down> gj
nore <Up> gk

" clear highlight
nnore <leader><CR> :noh<CR>

" In command-line mode, C-a jumps to beginning (to match C-e).
cnoremap <C-a> <Home>

" replace last search
nnore s :%s///g<Left><Left>
vnore s :s///g<Left><Left>

" Convert Ruby 1.8 hash rockets to 1.9 JSON style hashes.
command! -bar -range=% NotRocket execute '<line1>,<line2>s/:\(\w\+[?!]\?\)\s*=>/\1:/e' . (&gdefault ? '' : 'g')

" change cursor shape for urxvt, xterm,...
if exists('$TMUX')
    let &t_SI = "\<Esc>Ptmux;\<Esc>\<Esc>[6 q\<Esc>\\"
    let &t_SR = "\<Esc>Ptmux;\<Esc>\<Esc>[4 q\<Esc>\\"
    let &t_EI = "\<Esc>Ptmux;\<Esc>\<Esc>[2 q\<Esc>\\"
else
    let &t_SI = "\<Esc>[6 q"
    let &t_SR = "\<Esc>[4 q"
    let &t_EI = "\<Esc>[2 q"
endif

" next textobject {{{
" Motion for "next/last object". For example, "din(" would go to the next "()"
" pair and delete its contents.

onoremap an :<c-u>call <SID>NextTextObject('a', 'f')<cr>
xnoremap an :<c-u>call <SID>NextTextObject('a', 'f')<cr>
onoremap in :<c-u>call <SID>NextTextObject('i', 'f')<cr>
xnoremap in :<c-u>call <SID>NextTextObject('i', 'f')<cr>

onoremap al :<c-u>call <SID>NextTextObject('a', 'F')<cr>
xnoremap al :<c-u>call <SID>NextTextObject('a', 'F')<cr>
onoremap il :<c-u>call <SID>NextTextObject('i', 'F')<cr>
xnoremap il :<c-u>call <SID>NextTextObject('i', 'F')<cr>

function! s:NextTextObject(motion, dir)
  let c = nr2char(getchar())

  if c ==# "b"
      let c = "("
  elseif c ==# "B"
      let c = "{"
  elseif c ==# "d"
      let c = "["
  endif

  exe "normal! ".a:dir.c."v".a:motion.c
endfunction

" }}}

" unimpaired {{{
function! s:MapNextFamily(map,cmd)
  let map = '<Plug>unimpaired'.toupper(a:map)
  let cmd = '".(v:count ? v:count : "")."'.a:cmd
  let end = '"<CR>'.(a:cmd == 'l' || a:cmd == 'c' ? 'zv' : '')
  execute 'nnoremap <silent> '.map.'Previous :<C-U>exe "'.cmd.'previous'.end
  execute 'nnoremap <silent> '.map.'Next     :<C-U>exe "'.cmd.'next'.end
  execute 'nnoremap <silent> '.map.'First    :<C-U>exe "'.cmd.'first'.end
  execute 'nnoremap <silent> '.map.'Last     :<C-U>exe "'.cmd.'last'.end
  execute 'nmap <silent> ['.        a:map .' '.map.'Previous'
  execute 'nmap <silent> ]'.        a:map .' '.map.'Next'
  execute 'nmap <silent> ['.toupper(a:map).' '.map.'First'
  execute 'nmap <silent> ]'.toupper(a:map).' '.map.'Last'
  if exists(':'.a:cmd.'nfile')
    execute 'nnoremap <silent> '.map.'PFile :<C-U>exe "'.cmd.'pfile'.end
    execute 'nnoremap <silent> '.map.'NFile :<C-U>exe "'.cmd.'nfile'.end
    execute 'nmap <silent> [<C-'.a:map.'> '.map.'PFile'
    execute 'nmap <silent> ]<C-'.a:map.'> '.map.'NFile'
  endif
endfunction

call s:MapNextFamily('a','')
call s:MapNextFamily('b','b')
call s:MapNextFamily('l','l')
call s:MapNextFamily('q','c')
call s:MapNextFamily('t','t')
" }}}

" toogle quickfix list {{{
function! GetBufferList()
  redir =>buflist
  silent! ls!
  redir END
  return buflist
endfunction

function! ToggleList(bufname, pfx)
  let buflist = GetBufferList()
  for bufnum in map(filter(split(buflist, '\n'), 'v:val =~ "'.a:bufname.'"'), 'str2nr(matchstr(v:val, "\\d\\+"))')
    if bufwinnr(bufnum) != -1
      exec(a:pfx.'close')
      return
    endif
  endfor
  if a:pfx == 'l' && len(getloclist(0)) == 0
      echohl ErrorMsg
      echo "Location List is Empty."
      return
  endif
  let winnr = winnr()
  exec(a:pfx.'open')
  if winnr() != winnr
    wincmd p
  endif
endfunction

nmap <silent> <leader>c :call ToggleList("Quickfix List", 'c')<CR>
" }}}

" PLUGIN
" disable logiPat tarPlugin gzip zipPlugin vimballPlugin {{{
let g:loaded_logipat       = 1

let g:loaded_tarPlugin     = 1
let g:loaded_tar           = 1

let loaded_gzip            = 1

let g:loaded_zipPlugin     = 1
let g:loaded_zip           = 1

let g:loaded_vimballPlugin = 1
let g:loaded_vimball       = 1

" }}}

" Ultisnips {{{
let b:forward_complete = 0
augroup load_us
    autocmd!
    autocmd InsertEnter * call plug#load('ultisnips') | autocmd! load_us
augroup END

function! g:UltiSnips_Complete()
    call UltiSnips#ExpandSnippet()
    if g:ulti_expand_res == 0
        if pumvisible()
            return b:forward_complete ? "\<C-n>" : "\<C-p>"
        else
            if s:WillComplete()
                let b:forward_complete = 0
                return "\<C-p>"
            else
                return "\<TAB>"
            endif
        endif
    endif
    return ""
endfunction

function! g:UltiSnips_Reverse()
    if pumvisible()
        return b:forward_complete ? "\<C-p>" : "\<C-n>"
    else
        return "\<S-TAB>"
    endif
endfunction

function! s:WillComplete()
    let line = getline('.')
    let cnum = col('.') - 1
    let post = line[cnum - 1:]

    for pattern in ['$', '\s']
        if post =~ '^' . pattern
            return 0
        endif
    endfor

    return 1
endfunction

function! g:WrapOmni()
    let b:forward_complete = 1
    return "\<C-X>\<C-O>\<C-P>"
endfunction

let g:UltiSnipsExpandTrigger = "<C-k>"
xnoremap <Tab> :call UltiSnips#SaveLastVisualSelection()<CR>gvs
inoremap <silent> <TAB> <C-R>=g:UltiSnips_Complete()<cr>
inoremap <silent><expr> <S-TAB> g:UltiSnips_Reverse()
inoremap <expr> <C-X><C-O> g:WrapOmni()

imap <Nul> <C-Space>
inoremap <expr> <C-Space> g:WrapOmni()
" }}}

" Fcitx {{{
command! Fcitx :call plug#load('fcitx.vim')
" }}}

" incsearch.vim {{{
map /  <Plug>(incsearch-forward)\v
map ?  <Plug>(incsearch-backward)\v
map g/ <Plug>(incsearch-stay)\v
" }}}

" Tslime {{{
let g:tslime_always_current_session = 1
let g:tslime_always_current_window = 1
vmap <C-c><C-c> <Plug>SendSelectionToTmux
nmap <C-c><C-c> <Plug>NormalModeSendToTmux
nmap <C-c>r <Plug>SetTmuxVars
" }}}

" Neomake {{{
autocmd! BufWritePost * Neomake
if !has('nvim')
    let g:neomake_ruby_enabled_makers = ['mri']
endif
" }}}

" vim-test {{{
nmap <silent> <leader>t :TestNearest<CR>
nmap <silent> <leader>T :TestFile<CR>
nmap <silent> <leader>a :TestSuite<CR>
nmap <silent> <leader>l :TestLast<CR>
nmap <silent> <leader>g :TestVisit<CR>
let test#strategy = "dispatch"
" }}}

" Markdown {{{
let g:markdown_fenced_languages = ['coffee', 'css', 'erb=eruby.html',
            \ 'javascript', 'json=javascript', 'ruby', 'sass', 'html', 'sh']
" }}}

" Liquid {{{
let g:liquid_highlight_types = g:markdown_fenced_languages +
            \ ['html+erb=eruby.html']
" }}}

" ctrlp setting {{{
let g:ctrlp_map = ''
nore <Leader>p :CtrlP<CR>
nore <Leader>; :CtrlPTag<CR>
nore <Leader>, :CtrlPBufTag<CR>
let g:ctrlp_clear_cache_on_exit = 0
if has('python')
    let g:ctrlp_match_func = { 'match': 'pymatcher#PyMatch' }
endif
let g:ctrlp_custom_ignore = {
            \ 'dir': '\v[\/]\.(git|hg|svn)$',
            \ 'file': '\.pyc$\|\.pyo$\|\.rbc$|\.rbo$\|\.class$\|\.o$\|\~$\',
            \ }
" If ag is available use it as filename list generator instead of 'find'
if executable("ag")
    set grepprg=ag\ --nogroup\ --nocolor
    let g:ctrlp_user_command = 'ag %s -i --nocolor --nogroup
                \ --ignore ''.git''
                \ --ignore ''.DS_Store''
                \ --ignore ''node_modules''
                \ --ignore ''vendor/plugins''
                \ --hidden -g ""'
    let g:ctrlp_use_caching = 0
endif
" }}}

" gundo {{{
nnore <F5> :GundoToggle<CR>
inore <F5> <ESC>:GundoToggle<CR>
" }}}

" git fugitive {{{
nnore <leader>gb :Gblame<CR>
nnore <leader>gg :Ggrep <C-R><C-W><CR>
" }}}

" split join {{{
let g:splitjoin_split_mapping = ''
let g:splitjoin_join_mapping = ''
nmap <Leader>j :SplitjoinJoin<cr>
nmap <Leader>s :SplitjoinSplit<cr>

" sets the arglist to contain each of the files referenced by the quickfix list
command! -nargs=0 -bar Qargs execute 'args' QuickfixFilenames()
function! QuickfixFilenames()
    " Building a hash ensures we get each buffer only once
    let buffer_numbers = {}
    for quickfix_item in getqflist()
        let buffer_numbers[quickfix_item['bufnr']] = bufname(quickfix_item['bufnr'])
    endfor
    return join(map(values(buffer_numbers), 'fnameescape(v:val)'))
endfunction
" }}}

" EasyAlign {{{
" Start interactive EasyAlign in visual mode (e.g. vipga)
xmap ga <Plug>(EasyAlign)

" Start interactive EasyAlign for a motion/text object (e.g. gaip)
nmap ga <Plug>(EasyAlign)
" }}}

" vim-indent-guides {{{
let g:indent_guides_auto_colors = 0
hi IndentGuidesOdd  ctermbg=black
hi IndentGuidesEven ctermbg=darkgrey
" }}}

" vim-rails {{{
let g:rails_projections = {
    \          "app/serializers/*_serializer.rb": {
    \              "command":   "serializer",
    \              "affinity":  "model",
    \              "test":          "spec/serializers/{}_spec.rb",
    \              "related":   "app/models/{}.rb",
    \              "template":  "class {camelcase|capitalize|colons}Serializer < ActiveModel::Serializer\nend"
    \          },
    \          "app/decorators/*_decorator.rb": {
    \              "command": "decorator",
    \              "affinity": "model",
    \              "test": "spec/decorators/%s_spec.rb",
    \              "related": "app/models/%s.rb",
    \              "template": "class %SDecorator < Draper::Decorator\nend"
    \          },
    \          "test/factories/*.rb": {
    \              "command":   "factory",
    \              "affinity":  "collection",
    \              "alternate": "app/models/{singular}.rb",
    \              "related":   "db/schema.rb#{}",
    \              "test":          "test/models/{singular}_test.rb",
    \              "template":  "FactoryGirl.define do\n  factory :{singular} do\n  end\nend",
    \              "keywords":  "factory sequence"
    \          },
    \          "spec/factories/*.rb": {
    \              "command":   "factory",
    \              "affinity":  "collection",
    \              "alternate": "app/models/{singular}.rb",
    \              "related":   "db/schema.rb#{}",
    \              "test":          "spec/models/{singular}_test.rb",
    \              "template":  "FactoryGirl.define do\n  factory :{singular} do\n  end\nend",
    \              "keywords":  "factory sequence"
    \          }
    \ }
" }}}

" emmet {{{
imap <C-K> <C-Y>,
imap <C-n> <C-Y>n
imap <C-p> <C-Y>N
" }}}

" Ruby completion
let g:rubycomplete_buffer_loading = 1
let g:rubycomplete_classes_in_global = 1
let g:rubycomplete_rails = 1

" Language setting
augroup language_settings
    autocmd!
    autocmd FileType liquid,markdown,text,txt setlocal tw=78 linebreak nolist spell
augroup END

if !exists('g:colors_name')
    let g:badwolf_darkgutter = 1
    colorscheme badwolf
endif

if filereadable(expand("~/.vimrc.local"))
    source ~/.vimrc.local
endif

" vim: ts=8 sts=4 sw=4 fdm=marker
